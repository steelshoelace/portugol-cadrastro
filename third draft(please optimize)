
inicio
//vou chorar
//decl das variaveis
   texto email[50], senha[50]
   inteiro opcao, f, t
   
//f - indice dos vetores
//t - quantos items existem dentro dos vetores
  
 //programa
    t <- 0
    f <- 0
    
    //loop principal do programa
    repete 
        escrever "\n{====M.E.N.U====}"
        escrever "\n1. CADASTRAR"
        escrever "\n2. LISTAR"
        escrever "\n3. EXCLUIR"
        escrever "\n4. EDITAR"
        escrever "\n5. APAGAR TODOS OS DADOS"
        escrever "\n6. SAIR", "\n"
        ler opcao
        
        //escolha
        escolhe opcao
            caso 1: 
                //registrar o cadastro
                se t < 50 entao
                    escrever "\nEmail para cadastrar: \n"
                    ler email[t]
                    escrever "Senha: \n"
                    ler senha[t]
                    t <- t + 1
                senao 
                 escrever "\nNão há mais espaço.\n"
                fimse
                
            caso 2:
                //listar cadastros
                escrever "\n=== Cadastros ===\n"
                se t = 0 entao
                    escrever "\nNão existem usuários cadastrados.\n"
                senao
                 para f de 0 ate t - 1 passo 1
                   escrever "\nNumeração: ", f
                   escrever "\nEmail: ", email[f]
                   escrever "\nSenha: ", senha[f], "\n"
                 proximo
                fimse
                
            caso 3:
                //excluir os cadastros
                se t > 0 entao 
                    escrever "\n Qual cadastro para apagar? "
                    ler f
                    se f >= 0 e f < t entao
                      para f de f ate t - 2 passo 1
                          email[f] <- email[f + 1]
                          senha[f] <- senha[f + 1]
                      proximo
                      t <- t - 1
                      escrever "\nFoi-se.\n"
                    senao
                     escrever "\nNão há cadastros nessa posição, para checar a posição, liste os cadastros no menu.\n"
                    fimse
                senao
                 escrever "\nNão há cadastros para excluir.\n"
                fimse
                
            caso 4:  
                //editar os cadastros
                se t > 0 entao
                    escrever "\nQual cadastro você quer editar? "
                    ler f
                    se f >= 0 e f < t entao
                      escrever "\nNovo email: "
                      ler email[f]
                      escrever "Nova senha: "
                      ler senha[f]
                      escrever "\nInformações editadas\n"
                    senao
                     escrever "\nNumeração inválida.\n"
                    fimse
                senao
                 escrever "\nNão há cadastros para editar.\n"
                fimse
                
            caso 5:    
            
             para f de 0 ate 49 passo 1
                email[f] <- ""
                senha[f] <- ""
             proximo
             t <- 0
             escrever "\nTodos os dados foram apagados\n"
            caso 6:
                //matar o programa 
                escrever "\nAté!\n"
                
            defeito:
                escrever "\nNão é uma das opções.\n"
                
        fimescolhe  
        
    ate opcao = 6
fim
